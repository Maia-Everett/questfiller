apply plugin: 'java'

version = '1.2.8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

task wrapper(type: Wrapper) {
	gradleVersion = '4.2'
}

repositories {
	jcenter()
	
	flatDir {
		dirs 'lib'
	}
}

dependencies {
	compile group: 'org.jsoup', name: 'jsoup', version: '1.10.2'
	compile group: 'com.samskivert', name: 'jmustache', version: '1.13'
	
	testCompile group: 'junit', name: 'junit', version: '4.12'
}

sourceSets {
	main {
		java {
			srcDirs "$buildDir/generated-src"
		} 
	} 
}

task generateSources {
	outputs.dir file("$buildDir/generated-src")
	
	doFirst {
		def srcFile = new File("$buildDir/generated-src/org/lucidfox/questfiller/model/Version.java")
		srcFile.parentFile.mkdirs()
		srcFile.write("""
package org.lucidfox.questfiller.model;
		
public class Version {
	public static String getVersion() { return "$project.version"; }
}
""")
	}
}

compileJava.dependsOn generateSources
compileJava.source generateSources.outputs.files, sourceSets.main.java

jar {
	from {
		(configurations.runtime).collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}

	manifest {
		attributes("Main-Class": "org.lucidfox.questfiller.QuestFillerApp" )
	}
}
