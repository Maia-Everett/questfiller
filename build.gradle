buildscript {
	ext.kotlin_version = '1.1.4'
	repositories {
		mavenCentral()
		flatDir dirs: "proguard"
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
		classpath ":proguard:"
	}
}

apply plugin: 'java'
apply plugin: 'kotlin'

version = '1.2.4'

sourceCompatibility = 1.8
targetCompatibility = 1.8

task wrapper(type: Wrapper) {
	gradleVersion = '4.2'
}

repositories {
	jcenter()
	
	flatDir {
		dirs 'lib'
	}
}

dependencies {
	testCompile group: 'junit', name: 'junit', version: '4.12'

	compile group: 'org.jsoup', name: 'jsoup', version: '1.10.2'
	compile group: 'com.samskivert', name: 'jmustache', version: '1.13'
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

sourceSets {
	main {
		java {
			srcDirs "$buildDir/generated-src"
		} 
	} 
}

task generateSources {
	outputs.dir file("$buildDir/generated-src")
	
	doFirst {
		def srcFile = new File("$buildDir/generated-src/org/lucidfox/questfiller/model/Version.java")
		srcFile.parentFile.mkdirs()
		srcFile.write("""
package org.lucidfox.questfiller.model;
		
public class Version {
	public static String getVersion() { return "$project.version"; }
}
""")
	}
}

compileJava.dependsOn generateSources
compileJava.source generateSources.outputs.files, sourceSets.main.java

jar {
	from {
		(configurations.runtime).collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}

	manifest {
		attributes("Main-Class": "org.lucidfox.questfiller.QuestFillerApp" )
	}
}

task proguard(type: proguard.gradle.ProGuardTask, dependsOn: 'jar') {
	injars "$buildDir/libs/questfiller-${project.version}.jar"
	outjars "$buildDir/app/questfiller-${project.version}.jar"
	libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
	libraryjars "${System.getProperty('java.home')}/lib/ext/jfxrt.jar"

	dontobfuscate()
	dontoptimize()

	keepattributes '*Annotation*'

	keepclasseswithmembers 'public class * { \
        public static void main(java.lang.String[]); \
    }'

	keepclassmembers 'enum * {  *; }'
	keepclassmembers 'interface * {  *; }'
	keepclassmembers "class * { *; }"
}